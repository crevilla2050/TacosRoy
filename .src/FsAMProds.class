' Gambas class file

Private tmpCantidad As Integer
Private hResult As Result
Private hConn As Connection

Private qString As String


Public Sub Run() As Boolean

  Return Not Me.ShowModal()

End

Public Sub btnOK_Click() 

  'Inserta en la DB el producto y cantidad seleccionados, para la orden asignada
  Try hConn = modConn.ConectarDB()
  If Error Then 
    Message.Error(Error.Text) 
   Else
   
    qString = "INSERT INTO `db_tacos_roy`.`tbl_prods_x_orden` (`int_orden_id`, `int_producto_id`, `int_cantidad`) VALUES (&1, &2, &3)"
    Try hConn.Exec(qString, modVariables.$intOrdenTmp, modVariables.$intProductoTmp, modVariables.$intCantidadProd)
    If Error Then 
      Message.Error(Error.Text)
      modVariables.$FsAMProdsReturn = False
      modVariables.$intProductoTmp = 0
      modVariables.$intCantidadProd = 0
      Me.Close(False)
    Endif
  Endif

  modVariables.$FsAMProdsReturn = True
  Me.Close(True)
  
End

Public Sub btnCancel_Click()

  modVariables.$intProductoTmp = 0
  modVariables.$intCantidadProd = 0
  
  modVariables.$FsAMProdsReturn = False
  Me.Close(False)

End


Public Sub Form_Open()

  Dim intTempCat As Integer
  Dim myButtons As Button
  
  intTempCat = FComandas.$idCategoriaTemp
  
  Try hConn = modConn.ConectarDB()
  If Error Then Message.Error(Error.Text)
  
  Try hResult = hConn.Exec("Select * FROM  `vw_products_catprecio` where `CatID` = &1", intTempCat)
  If Error Then Print Error.Text
  
  If hResult.Available Then
    For Each hResult
      myButtons = New Button(Me) As "KeyButton"
      myButtons.Font.Size = 10
      myButtons.Text = hResult!Producto & ": $" & hResult!Precio
      myButtons.Tag = hResult!ID
      myButtons.ToolTip = hResult!Precio
      myButtons.Reparent(buttonsBox)
      myButtons.Expand = True
    Next
    Me.Arrangement = Arrange.LeftRight
  Endif
  
  hConn.Close
  hConn = Null
  
  tmpCantidad = 0
  btnMenos.Enabled = False  

End

Public Sub KeyButton_Click()
  
  'Message.Info("Button Tag: " & Last.Tag & "\nButton Text: " & Last.Text)
  tmpCantidad = 1
  btnMenos.Enabled = False
  lblCantidad.Text = "Cantidad: 1"
  lblSeleccion.Text = "Seleccion: " & CStr(Last.Tag) & ", " & Last.Text
  
  modVariables.$intProductoTmp = Last.Tag
    
  If (tmpCantidad <= 0) Then btnMenos.Enabled = False
  
  modVariables.$intCantidadProd = tmpCantidad
  Print "$intProductoTmp = " & modVariables.$intProductoTmp 
End

Public Sub btnCantidad_Click()
  btnOK.Enabled = False
  
  Select Case Last.Tag
    Case "++"
      Inc tmpCantidad
    Case "--"
      tmpCantidad = tmpCantidad - 1
    Case Else
      tmpCantidad = tmpCantidad + CInt(Last.Tag)
  End Select
  
  lblCantidad.Text = "Cantidad: " & tmpCantidad
  
  If (tmpCantidad > 0) Then btnMenos.Enabled = True Else btnMenos.Enabled = False
  
  modVariables.$intCantidadProd = tmpCantidad
  Print "$intCantidadProd = " & modVariables.$intCantidadProd
  
  If (tmpCantidad > 0) Then btnOK.Enabled = True
End

