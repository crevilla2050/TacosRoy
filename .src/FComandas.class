' Gambas class file

Public $idCategoriaTemp As Integer
Private hConn As Connection
Private hResult As Result
Private qString As String
Private tmpOrden_prods_precio As Result


Public Sub Button7_Click()
  
  Dim sQString As String
  Dim hResultOrdenes As Result
  
  'Antes de cerrar, refrescar el grid en la pantalla principal
  sQString = "SELECT `id_orden_id` as 'Orden', `dt_horafecha_orden` as 'Hora', `int_mesa` as 'Mesa', `chr_status_orden` as 'Status' From `tbl_ordenes` where `tbl_ordenes`.`chr_status_orden` = 'Abierta' order by Orden ASC"
  Try FMain1.hResultOrdenes = hConn.Exec(sQString)
  If Error Then 
    Print Error.Text
  Else
    'Llena el grid con las ordenes abiertas
    FMain1.gridMainOrden.Rows.Count = FMain1.hResultOrdenes.Count
    
  Endif
  Me.Close

End

Public Sub Form_Open()

  Dim myControl As Control
  Dim myButtons As Button
  Dim tmpResult As Result
  
  'Pone el reloj a tiempo
  lblTimer.Text = modUtils.TimerClk_Timer()
  lblInfo.Text = "Orden: " & modVariables.$intOrdenTmp & ", Mesa: " & modVariables.$strMesaTemp
  
  'define the gridview layout
  gridComanda.header = GridView.Horizontal
  gridComanda.grid = True
  gridComanda.Rows.count = 0
  gridComanda.Columns.count = 5
  gridComanda.Columns[0].text = "Orden"
  gridComanda.Columns[1].text = "Producto"
  gridComanda.Columns[2].text = "Cantidad"
  gridComanda.Columns[3].text = "Precio"
  gridComanda.Columns[4].text = "IDPO"
  
  gridComanda.Columns[0].width = 0
  gridComanda.Columns[1].width = 185
  gridComanda.Columns[2].width = 80
  gridComanda.Columns[3].width = 85
  gridComanda.Columns[4].width = 0
  
  Try hConn = modConn.ConectarDB()
  If Error Then Print Error.Text
  
  qString = "Select * from tbl_categorias order by fl_orden"
  
  Try hResult = hConn.Exec(qString)
  If Error Then Print Error.Text
  
  If hResult.Available Then
    For Each hResult
      myButtons = New Button(Me) As "KeyButton"
      myButtons.Font.Size = 11
      myButtons.Picture = Button1.Picture
      myButtons.Text = hResult!chr_nombre_cat
      myButtons.Tag = hResult!id_categoria
      myButtons.ToolTip = hResult!chr_desc_cat
      myButtons.Reparent(boxCategorias)
      myButtons.Expand = True 
    Next
    
    Button1.Visible = False
  Endif
  
  Try tmpOrden_prods_precio = hConn.Exec("Select * from vw_orden_prods_precio where Orden = &1 order by `IDPO` Asc", modVariables.$intOrdenTmp)
  
  If tmpOrden_prods_precio.Available Then
    gridComanda.Rows.Count = tmpOrden_prods_precio.Count
  Endif

End

Public Sub KeyButton_Click()
  'Message.Info("Button Tag: " & Last.Tag & "\nButton Text: " & Last.Text)
  
  
  $idCategoriaTemp = Last.Tag
  FsAMProds.ShowModal()
  
  If (modVariables.$FsAMProdsReturn = True) Then
    
    hConn = modConn.ConectarDB()
    
    qString = "SELECT * FROM vw_orden_prods_precio where Orden = &1 order by `IDPO` Asc"  
    Try tmpOrden_prods_precio = hConn.Exec(qString, modVariables.$intOrdenTmp)
    If Error Then Message.Error(Error.text) Else gridComanda.Rows.Count = tmpOrden_prods_precio.Count
    
  Endif

End

Public Sub gridComanda_Data(Row As Integer, Column As Integer)

Dim tmpString As String

  tmpOrden_prods_precio.moveTo(Row)
  
  If (Column = 3) Then 
    tmpString = Str(tmpOrden_prods_precio[gridComanda.Columns[column].text])
    'gridComanda.Data.text = Format(tmpString, "($$,#.###)")
    gridComanda.Data.text = tmpString
  Else
    gridComanda.Data.text = Str(tmpOrden_prods_precio[gridComanda.Columns[column].text])  
  Endif
  
End

Public Sub Timer1_Timer()

  lblTimer.Text = modUtils.TimerClk_Timer()

End


Public Sub gridComanda_Click()

  tmpOrden_prods_precio.MoveTo(gridComanda.Row)
  lblMessage.Text = tmpOrden_prods_precio[1] & ", Cantidad: " & tmpOrden_prods_precio[2]
  gridComanda.Rows.Select(tmpOrden_prods_precio.Index)

End

Public Sub btnEliminar_Click()

  Dim sQuery As String
  Dim rRecordOrder As Result

  If (Message.Question("Desea Eliminar el producto: " & gb.Cr & tmpOrden_prods_precio[1] & ", " & tmpOrden_prods_precio[2] & gb.Cr & " de la orden? ", " S Ã­, Eliminar", "No, Cancelar") = 1) Then
    
    tmpOrden_prods_precio.MoveTo(gridComanda.Row)
    sQuery = "DELETE FROM `db_tacos_roy`.`tbl_prods_x_orden` WHERE `tbl_prods_x_orden`.`id_prod_x_orden` = &1"  
    
    Print sQuery & ", " & tmpOrden_prods_precio[4]
    Try hConn.Exec(sQuery, tmpOrden_prods_precio[4])
    If Error Then Message.Error(Error.text)
    
    gridComanda.Rows.Count = 0
    Try tmpOrden_prods_precio = hConn.Exec("Select * from vw_orden_prods_precio where Orden = &1 order by `IDPO` Asc", modVariables.$intOrdenTmp)
    gridComanda.Rows.Count = tmpOrden_prods_precio.Count
  Endif

End
