' Gambas class file

Private hResult As Result
Private hConn As Connection

Private Sub checaMesas(HBox As Container)
  Dim myControl As Control
  Dim hFont As Font
  
  For Each mycontrol In HBox.Children
    If (Object.GetProperty(mycontrol, "Tag")) = Last.Tag Then
      hFont = Font["Ubuntu,Bold,+2"]
      Object.SetProperty(myControl, "Font", hFont)
    Else
      hFont = Font["Ubuntu,Normal,-2"]
      Object.SetProperty(myControl, "Font", hFont)
    Endif
  Next
  
End



Public Sub Form_Open()

  Dim myButtons As Button
  Dim sQuery As String
  Dim intCnt As Integer
    
  intCnt = 1
  modVariables.i_intIDPersonal = 0
  sQuery = "Select * From `tbl_mesas` where `tbl_mesas`.`bit_mesa_activa` = True order by `tbl_mesas`.`id_mesa_id` Asc"
  
  Try hResult = modVariables.c_Conn.Exec(sQuery)
  If Error Then Print Error.Text
  
  If hResult.Available Then
    For Each hResult
      'If hResult["bit_domicilio"] = 1 Then modVariables.iDomicilio = hResult["id_mesa_id"]
      myButtons = New Button(Me) As "MesasButton"
      myButtons.Font.Size = 12
      myButtons.Text = hResult!chr_mesa
      myButtons.Tag = hResult!id_mesa_id
      myButtons.Expand = True
      'Print intCnt
      If (intCnt >= 1 And intCnt <= 6) Then myButtons.Reparent(HBox1)
      If (intCnt > 6 And intCnt <= 12) Then myButtons.Reparent(HBox2)
      If (intCnt > 12 And intCnt <= 18) Then myButtons.Reparent(HBox3)
      Inc intCnt
    Next
  Endif
  
  hResult = Null
  sQuery = "SELECT * FROM tbl_personal where bit_activo = 1 order by id_personal Asc"
  
  Try hResult = modVariables.c_Conn.Exec(sQuery)
  For Each hResult
    cmbMeseros.Add(hResult!chr_nombre_persona, hResult!id_personal)
  Next
  '
  hResult = Null
  sQuery = "SELECT * FROM tbl_tipos_precios"
  cmbTipoPrecios.Add("Elija tipo de precios...")
  Try hResult = modVariables.c_Conn.Exec(sQuery)
  For Each hResult
    cmbTipoPrecios.Add(hResult!chr_nombre_precio, hResult!id_tipo_precio)
  Next
  
  hResult = Null

Catch
  Message.error(Error.Text & ", " & Error.Where)  
  Print Error.Text & ", " & Error.Where
  hResult = Null
End

Public Sub MesasButton_Click()
  
Dim myControl As Control
Dim hFont As Font
Dim hResult As Result

  hResult = modVariables.c_Conn.Exec("SELECT * FROM `tbl_mesas` where id_mesa_id = &1", Last.Tag)

  If hResult <> Null Then 
    modVariables.s_strMesaTemp = hResult["chr_mesa"]
    modVariables.i_intIDMesaTemp = hResult["id_mesa_id"]
  Endif

  checaMesas(HBox1)
  checaMesas(HBox2)
  checaMesas(HBox3)
'  Print Last.Tag
  If modVariables.s_strMesaTemp = "A Domicilio" Then cmbTipoPrecios.Index = 2
  
  hResult = Null
  BtnMesaOK.Enabled = True
  btnMesaCancel.Enabled = True
  
Catch
  Message.error(Error.Text & ", " & Error.Where)  
  Print Error.Text & ", " & Error.Where
  hResult = Null
End

Public Sub btnMesaCancel_Click()

  If (Message.Question("Deseas CANCELAR la operación?", "Sí, cancelar", "No, continuar") = 1) Then
    modVariables.s_strMesaTemp = 0
    Me.Close
    Return
  Endif

End

Public Sub BtnMesaOK_Click()

Dim sQuery As String
Dim hResult As Result
Dim sTemp As String

  modVariables.i_intIDPersonal = cmbMeseros.Index + 1
  If modVariables.s_strMesaTemp <> "" And modVariables.i_intIDPersonal <> 0 Then 
    
    sQuery = "SELECT * FROM db_tacosRoy.tbl_mesas where `chr_mesa` = &1;"
    Try hResult = modVariables.c_Conn.Exec(sQuery, modVariables.s_strMesaTemp)
    
    If cmbTipoPrecios.Index > 0 Then
      modVariables.i_intTipoPrecio = cmbTipoPrecios.Index
    Else
      Message.Warning("Elija un tipo de precios por favor")
      cmbTipoPrecios.SetFocus
    Endif
    
    sTemp = "Mesa asignada:" & hResult["chr_mesa"] & "\nMesero: " & cmbMeseros.Text & "\nTipo Precio: " & cmbTipoPrecios.Text
    
    If hResult["chr_mesa"] = "A domicilio" Then 
      FBClientes.ShowModal()
      
      sTemp &= "\nOrden a Domicilio:\n"
      sTemp &= modVariables.r_gridClientes["Nombre"] & ", " & modVariables.r_gridClientes["Apellidos"]
    Endif
    
    'Insertar orden por cliente en tabla
    'modVariables.iClienteID -> modVariables.i_intOrdenID
    hResult = Null
    modVariables.c_Conn.Begin()
    hResult = modVariables.c_Conn.Create("tbl_ordenes_x_clientes")
    hResult!id_orden = modVariables.i_intOrdenID
    hResult!id_cliente = modVariables.iClienteID
    hResult.Update()
    modVariables.c_Conn.Commit()
    
    modVariables.i_intTipoPrecio = cmbTipoPrecios.Index
    sTemp &= "\nTipo Precio: " & cmbTipoPrecios.Text
    Message.Info(sTemp)
    
    
    Me.Close
  Else
    Message.Error("¡No se ha asignado una mesa, o falta asignar mesero!")
  Endif

Catch
  Message.error(Error.Text & ", " & Error.Where)  
  Print Error.Text & ", " & Error.Where
  
End
